apply plugin: 'com.android.application'

ext.extApiKeys = new Properties()
extApiKeys.load(new FileInputStream(file("../apikeys.properties")))

android {
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
    }

    compileSdkVersion 25
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "dptsolutions.com.giphysearch"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "GIPHY_API_KEY", "\"" + extApiKeys['giphy.debug'] + "\""
        buildConfigField "String", "GIPHY_BASE_URL", "\"http://api.giphy.com\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    final SUPPORT_LIBRARY_VERSION = '25.4.0'
    final OKHTTP_VERSION = '3.8.1'
    final DAGGER_VERSION = '2.11'
    final RETROFIT_VERSION = '2.3.0'
    final OKIO_VERSION = '1.13.0'
    final AUTOVALUE_VERSION = '1.4.1'
    final AUTOVALUE_PARCEL_EXTENSION_VERSION = '0.2.5'
    final AUTOVALUE_GSON_EXTENSION_VERSION = '0.4.6'
    final BUTTERKNIFE_VERSION = '8.6.0'
    final RXBINDING_VERSION = '1.0.1'
    final GLIDE_VERSION = '4.0.0-RC1'
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-vector-drawable:$SUPPORT_LIBRARY_VERSION"
    compile 'com.google.android:flexbox:0.3.0'

    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    annotationProcessor "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    compile "com.google.dagger:dagger-android:$DAGGER_VERSION"
    compile "com.google.dagger:dagger-android-support:$DAGGER_VERSION"
    annotationProcessor "com.google.dagger:dagger-android-processor:$DAGGER_VERSION"

    compile "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    annotationProcessor "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"

    compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:adapter-rxjava:$RETROFIT_VERSION"

    compile "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    compile "com.squareup.okhttp3:logging-interceptor:$OKHTTP_VERSION"
    compile "com.squareup.okio:okio:$OKIO_VERSION"

    provided "com.google.auto.value:auto-value:$AUTOVALUE_VERSION"
    annotationProcessor "com.google.auto.value:auto-value:$AUTOVALUE_VERSION"
    annotationProcessor "com.ryanharter.auto.value:auto-value-gson:$AUTOVALUE_GSON_EXTENSION_VERSION"
    provided "com.ryanharter.auto.value:auto-value-gson:$AUTOVALUE_GSON_EXTENSION_VERSION"
    annotationProcessor "com.ryanharter.auto.value:auto-value-parcel:$AUTOVALUE_PARCEL_EXTENSION_VERSION"
    compile "com.ryanharter.auto.value:auto-value-parcel-adapter:$AUTOVALUE_PARCEL_EXTENSION_VERSION"

    compile "com.jakewharton.rxbinding:rxbinding:$RXBINDING_VERSION"
    compile "com.jakewharton.rxbinding:rxbinding-design:$RXBINDING_VERSION"

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.google.code.gson:gson:2.8.1'

    compile 'com.jakewharton.timber:timber:4.5.1'

    compile 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    compile ('org.aaronhe:threetenbp-gson-adapter:1.0.2', {
        exclude group: 'org.threeten', module: 'threetenbp'
    })

    compile 'io.reactivex:rxjava:1.3.0'
    compile 'io.reactivex:rxandroid:1.2.1'

    compile "com.github.bumptech.glide:glide:$GLIDE_VERSION"
    annotationProcessor "com.github.bumptech.glide:compiler:$GLIDE_VERSION"
    compile "com.github.bumptech.glide:okhttp3-integration:$GLIDE_VERSION"
    compile ("com.github.bumptech.glide:recyclerview-integration:$GLIDE_VERSION") {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }

    testCompile 'junit:junit:4.12'
}
